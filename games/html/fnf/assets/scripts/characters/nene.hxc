package funkin.play.character;

import funkin.play.character.AnimateAtlasCharacter;
import funkin.play.PlayState;
import flixel.FlxG;
import funkin.graphics.FunkinSprite;
import funkin.audio.visualize.ABotVis;

/**
 * One of two states that the A-Bot's pupils can be in.
 */
enum PupilState
{
  /**
   * The pupils are looking to the left.
   */
  LEFT;

  /**
   * The pupils are looking to the right.
   */
  RIGHT;
}

/**
 * The various states Nene can be in, controlling her animations.
 */
enum NeneState
{
  /**
   * Nene is in her default state. 'danceLeft' or 'danceRight' may be playing right now,
   * or maybe her 'combo' or 'drop' animations are active.
   *
   * Transitions:
   * If player health <= VULTURE_THRESHOLD, transition to PRE_RAISE.
   * If trainPassing is set to true, transition to HAIR_BLOWING.
   */
  DEFAULT;

  /**
   * Nene has recognized the player is at low health,
   * but has to wait for the appropriate point in the animation to move on.
   *
   * Transitions:
   * If player health > VULTURE_THRESHOLD, transition back to DEFAULT without changing animation.
   * If current animation is combo or drop, transition when animation completes.
   * If current animation is danceLeft, wait until frame 14 to transition to RAISE.
   * If current animation is danceRight, wait until danceLeft starts.
   * If trainPassing is set to true, transition to HAIR_BLOWING.
   */
  PRE_RAISE;

  /**
   * Nene is raising her knife.
   * When moving to this state, immediately play the 'raiseKnife' animation.
   *
   * Transitions:
   * Once 'raiseKnife' animation completes, transition to READY.
   * If trainPassing is set to true, transition to HAIR_BLOWING_RAISE.
   */
  RAISE;

  /**
   * Nene is holding her knife ready to strike.
   * During this state, hold the animation on the first frame, and play it at random intervals.
   * This makes the blink look less periodic.
   *
   * Transitions:
   * If the player runs out of health, move to the GameOverSubState. No transition needed.
   * If player health > VULTURE_THRESHOLD, transition to LOWER.
   * If trainPassing is set to true, transition to HAIR_BLOWING_RAISE.
   */
  READY;

  /**
   * Nene is about to lower her knife.
   * When moving to this state, play the 'lowerKnife' animation on the next beat.
   *
   * Transitions:
   * Once 'lowerKnife' animation completes, transition to DEFAULT.
   * If trainPassing is set to true, transition to HAIR_BLOWING.
   */
  LOWER;

  /**
   * Nene's hair is blowing to the side.
   * When moving to this state, immediately play the 'hairBlowNormal' animation.
   * If still in this state when the animation completes, loop the animation.
   *
   * Transitions:
   * If trainPassing is set to false, transition to HAIR_FALLING.
   */
  HAIR_BLOWING;

  /**
   * Nene's hair is returning to normal.
   * When moving to this state, immediately play the 'hairFallNormal' animation.
   *
   * Transitions:
   * Once 'hairFallNormal' animation completes, transition to DEFAULT.
   */
  HAIR_FALLING;

  /**
   * Nene's hair is blowing to the side, with her knife raised in the air.
   * When moving to this state, immediately play the 'hairBlowKnife' animation.
   * If still in this state when the animation completes, loop the animation.
   *
   * Transitions:
   * If trainPassing is set to false, transition to HAIR_FALLING_RAISE.
   */
  HAIR_BLOWING_RAISE;

  /**
   * Nene's hair is returning to normal, while her knife is raised in the air.
   * When moving to this state, immediately play the 'hairFallKnife' animation.
   *
   * Transitions:
   * Once 'hairFallKnife' animation completes, transition to READY.
   */
  HAIR_FALLING_RAISE;
}

class NeneBaseCharacter extends AnimateAtlasCharacter
{
  /**
   * Nene's animations are tracked in a simple state machine.
   * Given the current state and an incoming event, the state changes.
   */
  var currentState:NeneState = NeneState.DEFAULT;

  /**
   * The current state of A-Bot's pupils, tracked in a simple state machine.
   */
  var pupilState:PupilState = PupilState.RIGHT;

  /**
   * At this amount of life, Nene will raise her knife.
   */
  final VULTURE_THRESHOLD:Float = 0.25 * 2;

  /**
   * Nene blinks every X beats, with X being randomly generated each time.
   * This keeps the animation from looking too periodic.
   */
  /**
   * The minimum amount of time Nene will blink for.
   */
  final MIN_BLINK_DELAY:Int = 3;

  /**
   * The maximum amount of time Nene will blink for.
   */
  final MAX_BLINK_DELAY:Int = 7;

  /**
   * A timer that counts down to zero.
   * When it reaches zero, Nene will blink.
   */
  var blinkCountdown:Int = MIN_BLINK_DELAY;

  /**
   * When true, Nene's hair will start blowing to the side, used in Week 3.
   * Depending on the state she's in, her hair will blow with or without her knife raised.
   */
  var trainPassing:Bool = false;

  public function new(characterId:String)
  {
    // This tells CharacterDataParser to load Nene instead of an invalid character!
    if (characterId == "UNKNOWN")
    {
      characterId = "nene";
    }

    super(characterId);
  }

  var abot:FunkinSprite;
  var abotViz:ABotVis;
  var stereoBG:FunkinSprite;
  var eyeWhites:FunkinSprite;
  var pupil:FunkinSprite;

  public override function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    stereoBG = new FunkinSprite(0, 0, Paths.image('characters/abot/stereoBG'));

    eyeWhites = new FunkinSprite().makeSolidColor(160, 60);

    pupil = FunkinSprite.createTextureAtlas(0, 0, "characters/abot/systemEyes", "shared");
    pupil.x = this.x;
    pupil.y = this.y;
    pupil.zIndex = this.zIndex - 5;

    pupil.anim.onFrameChange.add(function(name:String, frameNumber:Int, frameIndex:Int)
    {
      if (frameNumber == 16)
      {
        pupil.anim.pause();
      }
    });

    abot = new ABotAtlasSprite(0, 0);
    abot.x = this.x;
    abot.y = this.y;
    abot.zIndex = this.zIndex - 1;

    abotViz = new ABotVis(FlxG.sound.music, false);
    abotViz.x = this.x;
    abotViz.y = this.y;
    abotViz.zIndex = abot.zIndex + 1;
    abotViz.alpha = 0;
  }

  public override function dance(forceRestart:Bool):Void
  {
    if (abot?.anim != null)
    {
      // we start on frame 1, since from Flash the symbol has a non-bumpin frame on frame 0
      abot.anim.play("", true, false, 1);
    }

    // Then, perform the appropriate animation for the current state.
    switch (currentState)
    {
      case NeneState.DEFAULT:
        if (hasDanced)
        {
          playAnimation('danceRight', forceRestart);
        }
        else
        {
          playAnimation('danceLeft', forceRestart);
        }
        hasDanced = !hasDanced;
      case NeneState.PRE_RAISE:
        playAnimation('danceLeft', false);
        hasDanced = false;
      case NeneState.READY:
        if (blinkCountdown == 0)
        {
          playAnimation('idleKnife', false);
          blinkCountdown = FlxG.random.int(MIN_BLINK_DELAY, MAX_BLINK_DELAY);
        }
        else
        {
          blinkCountdown--;
        }
      case NeneState.LOWER:
        if (getCurrentAnimation() != 'lowerKnife')
        {
          playAnimation('lowerKnife');
        }
      default:
        // In other states, don't interrupt the existing animation.
    }
  }

  /**
   * Called when the chart hits a song event.
   */
  public override function onSongEvent(scriptEvent:SongEventScriptEvent)
  {
    super.onSongEvent(scriptEvent);
    if (scriptEvent.eventData.eventKind == "FocusCamera")
    {
      var eventProps = scriptEvent.eventData.value;
      switch (Std.parseInt(eventProps.char))
      {
        case 0:
          movePupilsRight();
        case 1:
          movePupilsLeft();
        default:
      }
    }
  }

  function movePupilsLeft():Void
  {
    trace('Move pupils left');
    pupil.anim.play('', true, false, 0);
  }

  function movePupilsRight():Void
  {
    trace('Move pupils right');
    pupil.anim.play('', true, false, 17);
  }

  function moveByNoteKind(kind:String):Void
  {
    // Force ABot to look where the action is happening.
    switch (event.note.kind)
    {
      case "weekend-1-lightcan":
        movePupilsLeft();
      case "weekend-1-kickcan":
        // movePupilsLeft();
      case "weekend-1-kneecan":
        // movePupilsLeft();
      case "weekend-1-cockgun":
        movePupilsRight();
      case "weekend-1-firegun":
        // movePupilsRight();
      default: // Nothing
    }
  }

  public override function onNoteHit(event:HitNoteScriptEvent):Void
  {
    super.onNoteHit(event);
    moveByNoteKind(event.note.kind);
  }

  public override function onNoteMiss(event:NoteScriptEvent):Void
  {
    super.onNoteMiss(event);
    moveByNoteKind(event.note.kind);
  }

  var hasSetupAbot:Bool = false;

  public override function onUpdate(event:UpdateScriptEvent):Void
  {
    super.onUpdate(event);

    synchronizeShader();

    // Set the visibility of ABot to match Nene's.
    abot.visible = this.visible;
    pupil.visible = this.visible;
    eyeWhites.visible = this.visible;
    stereoBG.visible = this.visible;

    if (pupil?.anim?.isPlaying)
    {
      switch (pupilState)
      {
        case PupilState.RIGHT:
          if (pupil.anim.curFrame >= 17)
          {
            trace('Done moving pupils left');
            pupilState = PupilState.LEFT;
            pupil.anim.pause();
          }

        case PupilState.LEFT:
          if (pupil.anim.curFrame >= 30)
          {
            trace('Done moving pupils right');
            pupilState = PupilState.RIGHT;
            pupil.anim.pause();
          }
      }
    }

    // refreshes just for the zIndex shit!
    if (!hasSetupAbot)
    {
      setupAbot();
    }

    if (shouldTransitionState())
    {
      transitionState();
    }
  }

  function setupAbot():Void
  {
    abot.x = this.x - 95 - (-this.globalOffsets[0] * this.scale.x);
    abot.y = this.y + 384 - (-this.globalOffsets[1] * this.scale.y);
    abot.zIndex = this.zIndex - 10;

    currentStage.add(abot);

    abotViz.x = abot.x + 207;
    abotViz.y = abot.y + 84;
    abotViz.zIndex = abot.zIndex - 1;
    currentStage.add(abotViz);

    eyeWhites.x = abot.x + 40;
    eyeWhites.y = abot.y + 250;
    eyeWhites.zIndex = abot.zIndex - 10;
    currentStage.add(eyeWhites);

    pupil.x = abot.x + 50;
    pupil.y = abot.y + 238;
    pupil.zIndex = eyeWhites.zIndex + 5;
    currentStage.add(pupil);

    stereoBG.x = abot.x + 150;
    stereoBG.y = abot.y + 30;
    stereoBG.zIndex = abot.zIndex - 8;
    currentStage.add(stereoBG);

    currentStage.refresh();
    hasSetupAbot = true;
  }

  var currentShader:FlxRuntimeShader = null;

  function synchronizeShader():Void
  {
    if (currentShader == this.shader) return;

    currentShader = this.shader;

    abot.shader = currentShader;
    abotViz.shader = currentShader;
    stereoBG.shader = currentShader;
    eyeWhites.shader = currentShader;

    for (member in abotViz.members)
    {
      member.shader = currentShader;
    }

    trace("Synchronized shader between children!");
  }

  public override function onSongStart(event:ScriptEvent):Void
  {
    abotViz.snd = FlxG.sound.music;
    abotViz.initAnalyzer();
    abotViz.alpha = 1;
  }

  public override function onSongRetry(event:SongRetryEvent):Void
  {
    super.onSongRetry();
    abotViz.dumpSound();
  }

  public override function onSongEnd(event:ScriptEvent):Void
  {
    super.onSongEnd();
    abotViz.dumpSound();
  }

  var animationFinished:Bool = false;

  override function onAnimationFinished(name:String)
  {
    super.onAnimationFinished(name);

    switch (currentState)
    {
      case NeneState.RAISE, NeneState.LOWER, NeneState.HAIR_BLOWING, NeneState.HAIR_FALLING, NeneState.HAIR_BLOWING_RAISE, NeneState.HAIR_FALLING_RAISE:
        animationFinished = true;
        transitionState();
      default:
        // Ignore.
    }
  }

  override function onAnimationFrame(name:String, frameNumber:Int, frameIndex:Int)
  {
    super.onAnimationFrame(name, frameNumber, frameIndex);

    switch (currentState)
    {
      case NeneState.PRE_RAISE:
        if (name == "danceLeft" && frameNumber == 13)
        {
          animationFinished = true;
          transitionState();
        }
      default:
        // Ignore.
    }
  }

  function shouldTransitionState():Bool
  {
    return currentStage.getBoyfriend()?.characterId != "pico-blazin";
  }

  function checkTrainPassing(raised:Bool = false):Void
  {
    if (!trainPassing) return;

    if (raised)
    {
      currentState = NeneState.HAIR_BLOWING_RAISE;
      playAnimation('hairBlowKnife');
      animationFinished = false;
    }
    else
    {
      currentState = NeneState.HAIR_BLOWING;
      playAnimation('hairBlowNormal');
      animationFinished = false;
    }
  }

  function transitionState():Void
  {
    if (PlayState.instance == null) return;

    switch (currentState)
    {
      case NeneState.DEFAULT:
        if (PlayState.instance.health <= VULTURE_THRESHOLD)
        {
          currentState = NeneState.PRE_RAISE;
        }
        else
        {
          currentState = NeneState.DEFAULT;
        }
        checkTrainPassing();
      case NeneState.PRE_RAISE:
        if (PlayState.instance.health > VULTURE_THRESHOLD)
        {
          currentState = NeneState.DEFAULT;
        }
        else if (animationFinished)
        {
          currentState = NeneState.RAISE;
          playAnimation('raiseKnife');
          animationFinished = false;
        }
        checkTrainPassing();
      case NeneState.RAISE:
        if (animationFinished)
        {
          currentState = NeneState.READY;
          animationFinished = false;
        }
        checkTrainPassing(true);
      case NeneState.READY:
        if (PlayState.instance.health > VULTURE_THRESHOLD)
        {
          currentState = NeneState.LOWER;
          // lowerKnife will play on the next beat, so it syncs up properly
        }
        checkTrainPassing(true);
      case NeneState.LOWER:
        if (animationFinished)
        {
          currentState = NeneState.DEFAULT;
          animationFinished = false;
        }
        checkTrainPassing();
      case NeneState.HAIR_BLOWING:
        if (!trainPassing)
        {
          currentState = NeneState.HAIR_FALLING;
          playAnimation('hairFallNormal');
          animationFinished = false;
        }
        else if (animationFinished)
        {
          playAnimation('hairBlowNormal');
          animationFinished = false;
        }
      case NeneState.HAIR_FALLING:
        if (animationFinished)
        {
          currentState = NeneState.DEFAULT;
          animationFinished = false;
        }
      case NeneState.HAIR_BLOWING_RAISE:
        if (!trainPassing)
        {
          currentState = NeneState.HAIR_FALLING_RAISE;
          playAnimation('hairFallKnife');
          animationFinished = false;
        }
        else if (animationFinished)
        {
          playAnimation('hairBlowKnife');
          animationFinished = false;
        }
      case NeneState.HAIR_FALLING_RAISE:
        if (animationFinished)
        {
          currentState = NeneState.READY;
          animationFinished = false;
        }
      default:
        currentState = NeneState.DEFAULT;
    }
  }
}
